apply {
	plugin 'java'
	plugin 'groovy'
}

logger.quiet "Building Gripes-Web"

group = 'gripes'
archivesBaseName = 'gripes-web'
version = '0.1.7'

configurations {
	provided {  }
}

repositories {
    mavenCentral()
    mavenRepo urls: "http://www.gripes-project.org/libs"
    mavenRepo name: "jboss-snapshots", urls: "http://snapshots.jboss.org/maven2/"
}

sourceSets {
	main {
		compileClasspath += configurations.provided 
	    java { srcDir 'src/main/java' }
		groovy { srcDir 'src/main/groovy' }
	    resources { srcDir 'src/main/resources' }
	}
	test {
	    java { srcDir 'test/main/java' }
		groovy { srcDir 'test/main/groovy' }
	    resources { srcDir 'test/main/resources' }
	}
}

configurations {
	ftpAntTask
}

dependencies {
	//** Gradle API
	provided gradleApi()
	
	//** Ant FTP
	ftpAntTask module("org.apache.ant:ant-commons-net:1.7.0") {
        module("commons-net:commons-net:1.4.1") {
            dependencies("oro:oro:2.0.8@jar")
        }
    }
	
	//** Logging
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.5.11'
	compile group: 'ch.qos.logback', name: 'logback-core', version: '0.9.19'
	compile group: 'ch.qos.logback', name: 'logback-access', version: '0.9.19'
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '0.9.19'
	
	//** Groovy
	groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.7.10'
	
	//** Servlet
	testCompile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
	provided group: 'org.mortbay.jetty', name: 'jetty', version: '6.1.26'
	provided group: 'javax.servlet', name: 'jsp-api', version: '2.0'

	//** Framework
	compile group: 'net.sourceforge.stripes', name: 'stripes', version: '1.5.6'
	
	//** Database
	testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.0.0'
	compile group: 'org.hibernate', name: 'hibernate-core', version: '4.0.0.CR6'
	compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '4.0.0.CR6'
	compile group: 'org.hibernate', name: 'hibernate-c3p0', version: '4.0.0.CR6'
	
	//** Testing
	testCompile group: 'junit', name: 'junit', version: '4.8.2'

	compile project(":gripersist")
	
	compile name: 'stripesstuff', version: '0.1'
}

uploadArchives {
    repositories {
       flatDir(dirs: file('repos'))
    }
}
uploadArchives.doLast {
	ant {
		taskdef(name: 'ftp',
                classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
                classpath: configurations.ftpAntTask.asPath)

		ftp(action: "mkdir",
			server: "www.gripes-project.org", 
			remotedir: "${ftpUploadRoot}/libs/gripes/gripes-web/${version}",
			chmod: "0755",
			userid: ftpUploadUser,
			password: ftpUploadPassword)
		
		ftp(action: "put",
			server: "www.gripes-project.org", 
			remotedir: "${ftpUploadRoot}/libs/gripes/gripes-web/${version}",
			chmod: "0755",
			userid: ftpUploadUser,
			password: ftpUploadPassword) {
			fileset(dir:"repos", includes: "*${version}.*")
		}
    }
}